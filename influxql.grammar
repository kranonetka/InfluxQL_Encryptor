query                = statement (';' ws* statement)*
statement            = create_user_stmt / drop_database_stmt / select_stmt / create_database_stmt / show_databases_stmt
create_user_stmt     = 'CREATE USER' ws+ user_name ws+ 'WITH PASSWORD' ws+ password (ws+ 'WITH ALL PRIVILEGES')?
create_database_stmt = 'CREATE DATABASE' ws+ db_name
drop_database_stmt   = 'DROP DATABASE' ws+ db_name
show_databases_stmt  = 'SHOW DATABASES'
select_stmt          = 'SELECT' ws+ fields ws+ from_clause (ws+ where_clause)? (ws+ group_by_clause)?
fields               = (field (',' field)*) / '*'
field                = expr ''
binary_op            = '+' / '-' / '*' / '/' / '%' / '&' / '|' / '^' / 'AND' / 'OR' / '=' / '!=' / '<>' / '<='/ '<' / '>=' / '>'
expr                 = unary_expr (ws* binary_op ws* unary_expr)*
unary_expr           = ('(' expr ')') / (func '(' expr ')') / special / var_ref / string_lit / float_lit / duration_lit / int_lit / bool_lit / time_lit
func                 = 'min' / 'max' / 'mean' / 'count'
special              = ('time(' duration_lit ')') / 'time' / 'now()'
from_clause          = 'FROM' ws+ measurements
measurement          = (policy_name '.' measurement_name) / measurement_name
measurements         = measurement (',' measurement)*
measurement_name     = identifier ''
policy_name          = identifier ''
where_clause         = 'WHERE' ws+ expr
group_by_clause      = 'GROUP BY' ws+ dimensions (ws+ 'fill(' fill_option ')')?
dimension            = expr ''
dimensions           = dimension (',' dimension)*
fill_option          = 'null' / 'none' / 'previous' / 'linear' / int_lit / float_lit
user_name            = identifier ''
password             = string_lit ''
db_name              = identifier ''
identifier           = unquoted_identifier / quoted_identifier
string_lit           = "'" ~'[^\'\n]'* "'"
int_lit              = ('+' / '-')? ~'[1-9]' digit*
float_lit            = ('+' / '-')? (('.' digit+) / (digit+ '.' digit*))
bool_lit             = 'TRUE' / 'FALSE'
var_ref              = measurement
duration_lit         = int_lit ws? duration_unit
duration_unit        = 'u' / 'Âµ' / 'ms' / 's' / 'm' / 'h' / 'd' / 'w'
time_lit             = ("'" iso8601 "'") / int_lit
iso8601              = ~r"(-?(?:[1-9][0-9]*)?[0-9]{4})-(1[0-2]|0[1-9])-(3[01]|0[1-9]|[12][0-9])T(2[0-3]|[01][0-9]):([0-5][0-9]):([0-5][0-9])(\\.[0-9]+)?(Z)?"
unquoted_identifier  = letter (letter / digit)*
quoted_identifier    = '"' char+ '"'
char                 = ~r'[^"\n]'
letter               = ascii_letter / '_'
digit                = ~'[0-9]'
ascii_letter         = ~'[a-z]'i
ws                   = ~'\s'
