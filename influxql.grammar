query               = statement (';' ws* statement)*
statement           = create_user_stmt / drop_database_stmt
create_user_stmt    = 'CREATE USER' ws+ user_name ws+ 'WITH PASSWORD' ws+ password (ws+ 'WITH ALL PRIVILEGES')?
drop_database_stmt  = 'DROP DATABASE' ws db_name
select_stmt         = 'SELECT' ws+ fields ws+ from_clause (ws+ where_clause)? (ws+ group_by_clause)?
fields              = field (',' field)*
field               = expr ''
binary_op           = '+' / '-' / '*' / '/' / '%' / '&' / '|' / '^' / 'AND' / 'OR' / '=' / '!=' / '<>' / '<' / '<=' / '>' / '>='
expr                = unary_expr (binary_op unary_expr)*
unary_expr          = ('(' expr ')') / var_ref / string_lit / int_lit / float_lit / bool_lit
from_clause         = 'FROM' ws+ measurements
measurement         = measurement_name / (policy_name '.' measurement_name)
measurements        = measurement (',' measurement)*
measurement_name    = identifier ''
policy_name         = identifier ''
where_clause        = 'WHERE' expr
group_by_clause     = 'GROUP BY' ws+ dimensions 'fill(' fill_option ')'
dimension           = expr ''
dimensions          = dimension (',' dimension)*
fill_option         = 'null' / 'none' / 'previous' / 'linear' / int_lit / float_lit
user_name           = identifier ''
password            = string_lit ''
db_name             = identifier ''
identifier          = unquoted_identifier / quoted_identifier
string_lit          = "'" (letter / digit)* "'"
int_lit             = ('+' / '-')? ~'[1-9]' digit*
float_lit           = ('+' / '-')? (('.' digit+) / (digit+ '.' digit*))
bool_lit            = 'TRUE' / 'FALSE'
var_ref             = measurement
unquoted_identifier = letter (letter / digit)*
quoted_identifier   = '"' letter+ '"'
letter              = ascii_letter / '_'
digit               = ~'[0-9]'
ascii_letter        = ~'[a-z]'i
ws                  = ~'\s'
